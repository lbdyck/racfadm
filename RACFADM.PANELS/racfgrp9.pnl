)ATTR DEFAULT(%+_)
   _  TYPE(INPUT)   INTENS(HIGH) CAPS(ON)  JUST(LEFT)   HILITE(USCORE)
   }  TYPE(INPUT)   INTENS(HIGH) CAPS(OFF) JUST(LEFT)   HILITE(USCORE)
   {  TYPE(TEXT)    INTENS(LOW)  SKIP(ON)  COLOR(BLUE)
   +  TYPE(TEXT)    INTENS(LOW)  SKIP(ON)  COLOR(GREEN)
  01  TYPE(DATAOUT) COLOR(RED)
  02  TYPE(DATAOUT) COLOR(BLUE)
  03  TYPE(DATAOUT) COLOR(TURQ)  SKIP(ON)  HILITE(USCORE)
  04  TYPE(DATAOUT) COLOR(WHITE)
  05  TYPE(DATAOUT) COLOR(GREEN) SKIP(ON)
  ^   AREA(DYNAMIC) EXTEND(OFF)
)BODY EXPAND(\\)
+\-\(%RACFADM - Group Profiles+)\-\
%Command ===>_ZCMD
%
+Alter RACF%GROUP+profile: %ALTGROUP+command        %F1+- Segment syntax
+
+PROFILE %===>_GROUP   {
+
%Select the segments that you want to ALTER:
_z^DF      ^ {SMS data management and DASD storage (e.g., MGMTCLAS)
_z^OM      ^ {UNIX group attributes (e.g., GID)
_z^OV      ^ {Specifies OpenExtensions VM information
_z^TM      ^ {Tivoli Management Environment
)INIT
  .CURSOR  = GROUP
  .HELP    = #RACGRP9
  &gdfp     = ''
  &gomvs    = ''
  &govm     = ''
  &gtme     = ''
  .zvars   = '(gdfp gomvs govm gtme)'
  *rexx(*)
    DF = '05'x||'DFP'
    OM = '05'x||'OMVS'
    OV = '05'x||'OVM'
    TM = '05'x||'TME'

    rxrc=IRRXUTIL("EXTRACT","GROUP",group,"RACF","")
    if (word(rxrc,1) <> 0) then
      exit
    do i=1 to RACF.0 /* get the segment names */
      segment=RACF.i
      select
        when segment = 'DFP'      then DF  = '03'x||'DFP'||'02'x
        when segment = 'OMVS'     then OM  = '03'x||'OMVS'||'02'x
        when segment = 'OVM'      then OV  = '03'x||'OVM'||'02'x
        when segment = 'TME'      then TM  = '03'x||'TME'||'02'x
        otherwise nop
        end
      end
  *endrexx
)REINIT
  REFRESH(*)
)PROC
  VER (&GROUP,NB,NAME)
  *rexx(* gsegs)
    gsegs = ''
    if gdfp     <> '' then gsegs = gsegs 'DFP'
    if gomvs    <> '' then gsegs = gsegs 'OMVS'
    if govm     <> '' then gsegs = gsegs 'OVM'
    if gtme     <> '' then gsegs = gsegs 'TME'
  *endrexx
)END
/*--------------------------------------------------------------------*/
/*                             Comments                               */
/*--------------------------------------------------------------------*/
/*                                                                    */
/*  1) To display/use this panel, the user must have the Settings     */
/*     (menu option 0) turned on:                                     */
/*       Administrator ..... Y                                        */
/*       Admin RACF API .... Y                                        */
/*                                                                    */
/*  2) Descriptive text from Robert S. Hansel                         */
/*     https://www.rshconsulting.com/                                 */
/*                                                                    */
/*--------------------------------------------------------------------*/
